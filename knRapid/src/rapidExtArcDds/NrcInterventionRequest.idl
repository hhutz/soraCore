#ifndef rapid_ext_arc_NrcInterventionRequest_idl
#define rapid_ext_arc_NrcInterventionRequest_idl

/*
 * Copyright (c) 2015 United States Government as represented by the
 * Administrator of the National Aeronautics and Space Administration.
 * All Rights Reserved.
 */

#include "Message.idl"

module rapid {
  module ext {
    module arc {

      enum Category {
        CATEGORY_UNDEFINED,
        CATEGORY_COLLISION,
        CATEGORY_CONSTRUCTION_ZONE,
        CATEGORY_ROAD_OBSTRUCTION,
        CATEGORY_MECHANICAL_FAILURE,
        CATEGORY_SENSOR_FAILURE,
        CATEGORY_PLANNING_FAILURE,
        CATEGORY_TRAFFIC_CONTROLLER
      };
      typedef sequence<Category,8> CategorySequence;
      
      enum Severity {
        SEVERITY_IGNORE, 
        SEVERITY_LOW, 
        SEVERITY_MEDIUM, 
        SEVERITY_HIGH
      };

      //@copy-c-declaration class NrcInterventionRequestTypeSupport;
      //@copy-c-declaration class NrcInterventionRequestDataWriter;
      //@copy-c-declaration class NrcInterventionRequestDataReader;
      //@copy-c-declaration struct NrcInterventionRequestSeq;

      //@copy-declaration /**
      //@copy-declaration  * NrcInterventionRequest 
      //@copy-declaration  */
      valuetype NrcInterventionRequest : Message {

        //@copy-c-declaration #if RTI_DDS_VERSION_MAJOR < 4 || (RTI_DDS_VERSION_MAJOR == 4  && RTI_DDS_VERSION_MINOR < 5) || (RTI_DDS_VERSION_MAJOR == 4  && RTI_DDS_VERSION_MINOR  == 5 && RTI_DDS_VERSION_RELEASE != 'f' )
        //@copy-c-declaration typedef NrcInterventionRequestTypeSupport TypeSupport;
        //@copy-c-declaration typedef NrcInterventionRequestDataWriter DataWriter;
        //@copy-c-declaration typedef NrcInterventionRequestDataReader DataReader;
        //@copy-c-declaration typedef NrcInterventionRequestSeq Seq;
        //@copy-c-declaration #endif
        //@copy-c-declaration typedef NrcInterventionRequest Type;
        
          /** 
           * Unique id for situation. Updates to same situation will use same requestId 
           */
          public long     requestId;  //@key
          /** 
           * How urgently an intervention is required. Scale of 0-n (TBD) 
           */
          public long     urgency;
          /** 
           * Situation categories that led to intervention request. 
           * Each request should have at least one category. A situation
           * may be a combination of categories, e.g. roadwork blocking
           * the road with a flag person would be: 
           * CATEGORY_ROAD_OBSTRUCTION, CATEGORY_CONSTRUCTION_ZONE, CATEGORY_TRAFFIC_CONTROLLER
           */
          public CategorySequence category;
          /** 
           * Severity of situation, e.g. if category is COLLISION, 
           * SEVERITY_LOW would be tapping a garbage can
           * SEVERITY_HIGH would be a freeway pileup
           */
          public Severity severity;
          /**
           * Optional message to convey more information about the request. 
           * E.g. if category is CATEGORY_SENSOR_FAILURE, message could 
           * contain what sensor(s) failed
           */
          public String128 message; 
          /**
           * json encoded string with proposed solution (if any)
           */
          public String2K solution;
          
        };
    };
  };
};

#endif // rapid_ext_arc_NrcInterventionRequest_idl
